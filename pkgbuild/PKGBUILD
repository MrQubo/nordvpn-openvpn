# Maintainer: Nicolas Stinus <nicolas.stinus@gmail.com>

pkgname=nordvpn-openvpn
pkgver=0.1.43
pkgrel=1
pkgdesc="OpenVPN helper script for nordvpn.com"
arch=(any)
url="https://github.com/MrQubo/nordvpn-openvpn"
license=('MIT')
depends=('openvpn'
         'systemd'
         'openvpn-update-resolv-conf-git'
         'curl'
         'unzip'
         'sudo'
         'coreutils'
         'bc'
         'fping')
optdepends=('iputils: run ping and rank functions'
            'vpnfailsafe-git: use instead of update-resolv-conf if available'
            'python-pandas: run nordvpn-openvpn infos command'
            'bash-completion: auto-complete commands'
            'jq: pick recommended server for "start" command')
makedepends=('coreutils' 'asciidoc' 'git')
provides=('nordvpn-openvpn')
source=("git+https://github.com/MrQubo/nordvpn-openvpn.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    git --git-dir $(pwd)/nordvpn-openvpn/.git describe --always --long --match "v?.?" \
        | sed 's/-/./' \
        | cut -d '-' -f 1 \
        | sed 's/v//'
}

build() {
    sed "s/@version@/$pkgver/g" nordvpn-openvpn/doc/man.asciidoc > man.asciidoc \
        && a2x --doctype manpage --format manpage man.asciidoc \
        && gzip -f nordvpn-openvpn.8
}

package() {
    install -D -m 644 nordvpn-openvpn.8.gz $pkgdir/usr/share/man/man8/nordvpn-openvpn.8.gz
    install -D -m 755 nordvpn-openvpn/src/nordvpn-openvpn $pkgdir/usr/bin/nordvpn-openvpn
    install -D -m 644 nordvpn-openvpn/src/servers.py $pkgdir/etc/openvpn/client/nordvpn-openvpn/servers.py
    chmod 750 $pkgdir/etc/openvpn/client $pkgdir/etc/openvpn/client/nordvpn-openvpn
    install -D -m 644 nordvpn-openvpn/completion/bash $pkgdir/usr/share/bash-completion/completions/nordvpn-openvpn
}
